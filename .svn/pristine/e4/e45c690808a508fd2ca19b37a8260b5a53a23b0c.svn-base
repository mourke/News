class Label extends Widget {

  private String text;
  private PFont font;
  private color textColor;
  private float maxWidth = Float.MAX_VALUE;
  private int numberOfLines = 1;

  public Label(String text, Rect frame) {
    super(frame);
    this.text = text;
    setBackgroundColor(color(255, 255, 255, 0));
    updateSize();
  }
  
  // pass `0` to get infinite number of lines.
  public void setNumberOfLines(int numberOfLines) throws IllegalArgumentException {
    if (numberOfLines < 0) throw new IllegalArgumentException("Number of lines cannot be less than 0");
    this.numberOfLines = numberOfLines;
  }
  
  public void setMaxWidth(float maxWidth) {
    this.maxWidth = maxWidth;
    updateSize();
  }

  public void setText(String text) {
    if (getTextSize(text) > maxWidth) return; // change this so it truncates instead of not setting anything
    this.text = text;
    updateSize();
  }

  public void setFont(PFont font) {
    this.font = font;
    updateSize();
  }

  public void setTextColor(color textColor) {
    this.textColor = textColor;
  }

  private void updateSize() {
    float textWidth = getTextSize(this.text);
    frame.width = textWidth > maxWidth ? maxWidth : textWidth;
  }
  
  private float getTextSize(String text) {
    if (font != null) textFont(font); // textFont must be called so an accurate size can be calculated.
    return textWidth(text);
  }

  public String getText() {
    return this.text;
  }

  public PFont font() {
    return this.font;
  }

  public void draw() {
    if (hidden) return;
    
    super.draw();

    fill(textColor);
    if (font != null) {
      textFont(font);
    }
    textAlign(CENTER, CENTER);

    text(text, frame.x + frame.width/2, frame.y + frame.height/2);
  }
}
